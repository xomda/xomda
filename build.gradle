plugins {
    id 'maven-publish'
}

allprojects {
    group "org.xomda"
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            name 'xomda common-java'
            url = uri("https://maven.pkg.github.com/xomda/common-java")
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv("USERNAME")
                password = project.findProperty('gpr.key') ?: System.getenv("TOKEN")
            }
        }
    }
}

subprojects {

    if (project.name.equals("lib") || project.name.equals("plugin")) return

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {

        publications {
            // will be published be "pluginMaven"
            if (project.name.equals("xomda-plugin-gradle")) return
            maven(MavenPublication) {
                version = libs.versions.xomda.xomda.get()
                group 'org.xomda'
                from components.java
            }
        }

        repositories {
            mavenLocal()
            maven {
                url = "https://maven.pkg.github.com/xomda/xomda"
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}